{
    textdomain "support";

    include "installation_ui.ycp";

    // support_question.ycp
    // present/ask question data

    term contents = `VBox (
		      // caption above view window
		      `LogView (`id(`hwdata), _("Hardware data"), 7, 0),
		      // caption above view window
		      `LogView (`id(`swdata), _("Software data"), 7, 0),
		      `VSpacing (1.0),
		      `RadioButtonGroup(`id(`rb),
			`VBox (
			`Left (`HBox (
			    // radio button
			    `RadioButton (`id(`online), _("transmission via Internet to SuSE Support"), true),
			    // radio button
			    `RadioButton (`id(`disk), _("save to disk"), false)
			  )
			)
		        )
		      )
		    );

    // helptext 1 of 3
    string help = UI(_("<p><b>Support Data</b></p>\n"));

	//helptext 2 of 3
        help = help + UI(_("<p>Here you will see an overview of the information you 
transmitted. The transfer takes place by HTTP uploading.</p>"));

	//helptext 3 of 3
        help = help + UI(_("<p>If you do not have Internet access on your Linux system, 
you can save the data instead to the hard drive (save to disk) and from there 
transfer the data to another computer by floppy disk.</p>"));

    // screen caption
    UI(`SetWizardContents (_("SuSE Support"), contents, help, true, true));

    string tmpdir = SCR(`Read(.target.tmpdir));

    string reg_file = "/var/lib/support/rc.registration";

//    string user_data = "REGCODE "+ lookup (regdata, "regcode", "-none-") + "\n";
//    user_data = user_data + "NAME "+lookup (regdata, "name1", "") + " " + lookup (regdata, "name2", "") + "\n";
//    user_data = user_data + "EMAIL "+lookup (regdata, "email", "") + "\n";
//    user_data = user_data + "\nREGISTRIERDATEN ENDE\n";
//    user_data = user_data + "---\n" + lookup (regdata, "question", "") + "\n---\n";
    string user_data = "---\n" + lookup (regdata, "question", "") + "\n---\n";
    string user_file = tmpdir+"/user.out";

    SCR(`Write(.target.string, user_file, user_data));

    string hw_data = "";
    string hw_file = tmpdir+"/hardware.out";
    if (lookup (regdata, "hwdata", false)) {
	SCR(`Execute(.target.bash, "/var/lib/support/eo.pl -u -p >" + hw_file));
	hw_data = hw_data + SCR(`Read(.target.string, hw_file));
    }

    string sw_data = "";
    string sw_file = tmpdir+"/software.out";
    if (lookup (regdata, "swdata", false)) {
	SCR(`Execute(.target.bash, "/var/lib/support/eo.pl -s -b -m -r -v -l -f -x -P -F >" + sw_file));
	sw_data = sw_data + SCR(`Read(.target.string, sw_file));
    }

    UI(`ChangeWidget(`id(`hwdata), `Value, hw_data));
    UI(`ChangeWidget(`id(`swdata), `Value, sw_data));

    any ret = UI (`UserInput());

    string all_files = reg_file + " " + user_file + " " + hw_file + " " + sw_file;

    if (ret == `next) {
	if (UI(`QueryWidget (`id(`online), `Value)) == true) {
	    if (SCR(`Execute(.target.bash, "/usr/lib/YaST2/bin/is_suse_there.sh")) != 0) {
		// cant connect to server, popup message 1 of 3
		string message_text = UI(_("Can't connect to SuSE support server."));
		// cant connect to server, popup message 2 of 3
		message_text = message_text + "\n" + UI(_("Please check your internet connection"));
		// cant connect to server, popup message 3 of 3
		message_text = message_text + "\n" + UI(_("or choose 'save to disk' and send a printed copy by mail."));
		UI(`DisplayMessage (message_text));
		ret = `again;
	    }
	    else {
		// popup message, about to connect to support server
		UI(`DisplayMessage (_("Sending data via Internet")));
		if (SCR(`Execute(.target.bash, "/var/lib/support/send_support_request.pl " + all_files)) != 0) {
		    // failed to upload data to server, popup message 1 of 3
		    string message_text = UI(_("Can't connect to SuSE support server."));
		    // failed to upload data to server, popup message 2 of 3
		    message_text = message_text + "\n" + UI(_("Please check your internet connection"));
		    // failed to upload data to server, popup message 3 of 3
		    message_text = message_text + "\n" + UI(_("or try again later."));
		    UI(`DisplayMessage (message_text));
		    ret = `again;
		}
	    }
	}
	else if (UI(`QueryWidget (`id(`disk), `Value)) == true) {
	    string disk_file = "/tmp/support.request";
	    // popup message about where the "saving to disk" file resides
	    string disk_message = sformat (UI(_("Saving data to %1")), disk_file);
	    UI(`DisplayMessage (disk_message));
	    SCR(`Execute(.target.bash, "/bin/cat " + all_files + " > " + disk_file));
	}
    }

    SCR(`Execute(.target.remove, hw_file));
    SCR(`Execute(.target.remove, sw_file));

    return ret;
}
