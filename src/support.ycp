/* 
 * 
 *
 * $Id$
 */

{
    textdomain "support";

    integer current_stage = -1;
    include "ui/wizard_dialog.ycp";
    include "ui/common_popups.ycp";
    include "ui/common_functions.ycp";
    include "ui/common_messages.ycp";
    include "support/registration.ycp";

    // regata is global !

    map regdata = read_registration ();

    UI(`CreateWizardDialog());
    UI(`ReplaceWizardAbortButton(`Empty()));

    // if no valid registration data present, ask !

    any retval = nil;

    // use string->locale propagation
    locale empty_locale = "";
    boolean ask_registration = (check_registration (regdata) != empty_locale);

    boolean go_on = true;

    while (go_on) {

	while (ask_registration) {
	    retval = CallFunction (`support_registration ());
	    if (retval != `next) {
		go_on = false;
		break;
	    }
	    else {
		locale check_message = check_registration (regdata);
		ask_registration = (check_message != _("NIL"));
		if (ask_registration == true) {
		    UI (`ErrorPopup (check_message));
		}
		else if (lookup (regdata, "changed", false)) {
		    // popup when saving data
		    UI (`MessagePopup (_("Saving new registration data")));
		    if (!write_registration (regdata)) {
			// popup when saving failed
			UI (`ErrorPopup (_("Saving of registration data failed !")));
		    }
		    break;
		}
		else {
		    break;
		}
	    }
	} // while (ask_registration)

	if (go_on == false)
	    break;

	retval = CallFunction (`support_question());
	if (retval == `ask_registration) {
	    ask_registration = true;
	}
	else if (retval == `back) {
	    retval = `abort;
	}
	else if (retval == `next) {
	    retval = `again;
	    // support_send might return `again, loop here
	    while (retval == `again) {
		retval = CallFunction (`support_send ());
	    }
	    if (retval == `next) {
		// just a final text to give user a 'good' fealing
		UI(`MessagePopup (_("Thank you for using SuSE support")));
		go_on = false;
	    }
	}

	if (retval == `abort) {
	    go_on = false;
	}
    }


    // --------------------------------------------------------------
 
    UI( `CloseDialog() );
  
    return `next;
}
