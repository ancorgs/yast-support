/* 
 * 
 *
 * $Id$
 */

{
    textdomain "support";

    integer current_stage = -1;
    include "installation_ui.ycp";

    include "support/registration.ycp";

    // regata is global !

    map regdata = read_registration ();

    UI(`CreateWizardDialog());
    UI(`ReplaceWizardAbortButton(`Empty()));

    // if no valid registration data present, ask !

    any retval = nil;
    // dummy string, dont translate
    boolean ask_registration = (check_registration (regdata) != _("NIL"));
    boolean go_on = true;

    while (go_on) {

	while (ask_registration) {
	    retval = CallFunction (`support_registration ());
	    if (retval == `abort) {
		go_on = false;
		break;
	    }
	    if (retval == `next) {
		ask_registration = false;
		if (lookup (regdata, "changed", false)) {
		    // popup when saving data
		    UI (`DisplayMessage (_("Saving new registration data")));
		    if (!write_registration (regdata)) {
			// popup when saving failed
			UI (`DisplayMessage (_("Saving of registration data failed !")));
		    }
		}
		break;
	    }
	} // while (ask_registration)

	if (go_on == false)
	    break;

	retval = CallFunction (`support_question());
	if (retval == `ask_registration) {
	    ask_registration = true;
	}
	else if (retval == `next) {
	    retval = CallFunction (`support_send ());
	    if (retval == `next) {
		// just a final text to give user a 'good' fealing
		UI(`DisplayMessage(_("Thank you for using SuSE support")));
		go_on = false;
	    }
	}

	if (retval == `abort) {
	    go_on = false;
	}
    }


    // --------------------------------------------------------------
 
    UI( `CloseDialog() );
  
    return `next;
}
