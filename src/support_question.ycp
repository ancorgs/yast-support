{
    textdomain "support";

    // support_question.ycp

    // present/ask question data

    term contents = `VBox (
			// frame heading
		      `Frame (_("Current registration data"),
			`HBox (
			  // label for regcode field
			  `Label (_("Registration Code:")),
			  `Label (lookup (regdata, "regcode", "")),
			  `HSpacing (0.5),
			  `Label (lookup (regdata, "name1", "")),
			  `Label (lookup (regdata, "name2", "")),
			  `HStretch (),
			  // push button for changeing registration data
			  `PushButton (`id(`ask_registration), _("&Change"))
			 )
		      ),
		      `VSpacing (1.0),
		      // checkbox for sending hardware data
		      `Left (`CheckBox (`id(`hwdata), _("&Send hardware data"), lookup (regdata, "hwdata", true))),
		      // checkbox for sending software data
		      `Left (`CheckBox (`id(`swdata), _("&Send software data"), lookup (regdata, "swdata", true))),
		      `VSpacing (1.0),
		      // label for multi line edit widget where user can
		      // describe the problem
		      `MultiLineEdit (`id(`question), _("&Your question for SuSE support:"), lookup (regdata, "question", "")),
		      `VStretch ()
		    );

        //helptext 1 of 4
    string help = UI(_("<p><b>Support Question</b></p>\n"));

        //helptext 2 of 4
        help = help + UI(_("<p>Please describe your problem briefly here, but still 
as thoroughly as you can. Exact error messages and previous action you have taken 
are especially important for us to know.</p>"));
 
        //helptext 3 of 4
        help = help + UI(_("<p>Please do not include more than one question in a mail. 
In other words, separate multiple requests, if that is the case, under different 
subject blocks, and write a specific request corresponding to each topic. This makes 
them easier to process and thus accelerates our response.</p>"));
 
        //helptext 4 of 4
        help = help + UI(_("<p>The hard- and software information serves to determine 
the basic system information and configuration on your computer. In case of doubt, 
this information can be helpful for support and should therefore only be de-activated 
with good reason.</p>"));

    // screen title
    UI(`SetWizardContents (_("SuSE Support"), contents, help, true, true));

    any ret = UI (`UserInput());

    if (ret != `abort) {
	regdata = add (regdata, "question", UI(`QueryWidget (`id(`question), `Value)));
	regdata = add (regdata, "hwdata", UI(`QueryWidget (`id(`hwdata), `Value)));
	regdata = add (regdata, "swdata", UI(`QueryWidget (`id(`swdata), `Value)));
    }

    return ret;
}
